module de.wwu.pi.fooddelivery.epcToApp.workflow.EpcToAppGenerator

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.generator.*

var targetDir = "src-gen"
var projectName = "FoodDeliveryX"
var baseProjectPath = "../${projectName}"
var modelPath = "../FoodDeliveryX-Model"

Workflow {
	bean = StandaloneSetup {
		//register UML metamodel
		registerGeneratedEPackage = "de.wwu.pi.epc.EpcPackage"
		extensionMap = {
			from = "epc"
			to = "de.wwu.pi.epc.EpcResourceFactoryImpl"
		}
	}
	
//	Problem with reading file encoding https://bugs.eclipse.org/bugs/show_bug.cgi?id=409069
	component = DirectoryCleaner {
		directory = targetDir
		exclude = ".git"
	}

	component = DirectoryCleaner {
		directory = "${baseProjectPath}-Web/${targetDir}"
		exclude = ".git"
		exclude = ".gitkeep"
	}
	
	component = DirectoryCleaner {
		directory = "${baseProjectPath}-Web/WebContent/web-gen"
		exclude = ".git"
		exclude = ".gitkeep"
	}
	
	component = EpcToAppGeneratorSupport {}
	
	//register uml reader
	component = org.eclipse.xtext.mwe.Reader {
		path = modelPath
		register = EpcToAppGeneratorSetup {}
		loadResource = {
			slot = "model"
		}
	}
	
	//register generator
	component = org.eclipse.xtext.generator.GeneratorComponent {
		register = EpcToAppGeneratorSetup {}
		slot = "model"
		outlet = {
			path = targetDir
		}
	}
}
